; SPIR-V
; Version: 1.0
; Generator: Khronos LLVM/SPIR-V Translator; 14
; Bound: 55
; Schema: 0
               OpCapability Addresses
               OpCapability Kernel
               OpCapability Int64
               OpCapability ImageBasic
               OpCapability LiteralSampler
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %test "image_flow_control_test" %gid
               OpDecorate %gid BuiltIn GlobalInvocationId
               OpDecorate %gid Constant
      %ulong = OpTypeInt 64 0
       %uint = OpTypeInt 32 0
      %upper = OpConstant %ulong 1048576
     %uint_0 = OpConstant %uint 0
    %v3ulong = OpTypeVector %ulong 3
    %ptr_gid = OpTypePointer Input %v3ulong
       %void = OpTypeVoid
%_ptr_CrossWorkgroup_uint = OpTypePointer CrossWorkgroup %uint
  %image2d_t = OpTypeImage %void 2D 0 0 0 0 Unknown ReadOnly
     %func_t = OpTypeFunction %void %_ptr_CrossWorkgroup_uint %image2d_t
       %bool = OpTypeBool
  %sampler_t = OpTypeSampler
%sampledimage_t = OpTypeSampledImage %image2d_t
     %v4uint = OpTypeVector %uint 4
     %v2uint = OpTypeVector %uint 2
      %float = OpTypeFloat 32
        %gid = OpVariable %ptr_gid Input
    %sampler = OpConstantSampler %sampler_t None 0 Nearest
  %coord_0_0 = OpConstantNull %v2uint
    %float_0 = OpConstant %float 0
       %test = OpFunction %void None %func_t
        %dst = OpFunctionParameter %_ptr_CrossWorkgroup_uint
      %image = OpFunctionParameter %image2d_t
      %entry = OpLabel
; This version does not pass through spirv-val, but still works on many implementations.
%sampledimage = OpSampledImage %sampledimage_t %image %sampler
      %gidv3 = OpLoad %v3ulong %gid Aligned 32
       %gid0 = OpCompositeExtract %ulong %gidv3 0
       %cond = OpSLessThan %bool %gid0 %upper
               OpBranchConditional %cond %then %endif
       %then = OpLabel
; This version should pass on all implementations, and passes through spirv-val.
;%sampledimage = OpSampledImage %sampledimage_t %image %sampler
%dst_wsampler = OpImageSampleExplicitLod %v4uint %sampledimage %coord_0_0 Lod %float_0    ; 0x11
%dst_nosampler = OpImageRead %v4uint %image %coord_0_0                                    ; 0x11
     %sumdst = OpIAdd %v4uint %dst_wsampler %dst_nosampler  ; 0x11 + 0x11 = 0x22
       %res0 = OpCompositeExtract %uint %sumdst 0           ; 0x22
  %dstformat = OpImageQueryFormat %uint %image              ; SignedInt8 = 7
       %res1 = OpIAdd %uint %res0 %dstformat                ; 0x22 + 7 = 0x29
   %dstorder = OpImageQueryOrder %uint %image               ; RGBA = 5
       %res2 = OpIAdd %uint %res1 %dstorder                 ; 0x29 + 5 = 0x2E
    %dstsize = OpImageQuerySizeLod %v2uint %image %uint_0   ; 1
   %dstwidth = OpCompositeExtract %uint %dstsize 0          ; 1
       %res3 = OpIAdd %uint %res2 %dstwidth                 ; 0x2E + 1 = 0x2F = 47
     %dstptr = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %dst %gid0
               OpStore %dstptr %res3 Aligned 4
               OpBranch %endif
      %endif = OpLabel
               OpReturn
               OpFunctionEnd
