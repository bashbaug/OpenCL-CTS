; SPIR-V
; Version: 1.0
; Generator: Khronos LLVM/SPIR-V Translator; 14
; Bound: 43
; Schema: 0
               OpCapability Addresses
               OpCapability Linkage
               OpCapability Kernel
               OpCapability Int64
               OpCapability ShaderClockKHR
               OpExtension "SPV_KHR_shader_clock"
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %clock_sub_group "clock_read_sub_group_hilo"
               OpDecorate %buf Alignment 4
      %ulong = OpTypeInt 64 0
       %uint = OpTypeInt 32 0
     %v2uint = OpTypeVector %uint 2
     %uint_0 = OpConstant %uint 0
     %uint_1 = OpConstant %uint 1
    %ulong_1 = OpConstant %ulong 1
    %ulong_2 = OpConstant %ulong 2
    %ulong_3 = OpConstant %ulong 3
%scope_subgroup = OpConstant %uint 3
       %void = OpTypeVoid
%_ptr_CrossWorkgroup_uint = OpTypePointer CrossWorkgroup %uint
   %functype = OpTypeFunction %void %_ptr_CrossWorkgroup_uint
       %bool = OpTypeBool
%clock_sub_group = OpFunction %void None %functype
        %buf = OpFunctionParameter %_ptr_CrossWorkgroup_uint
  %funcstart = OpLabel
      %buf_1 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_1
      %buf_2 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_2
      %buf_3 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_3
      %start = OpReadClockKHR %v2uint %scope_subgroup
               OpBranch %looptop
    %looptop = OpLabel
          %i = OpPhi %uint %uint_0 %funcstart %next_i %looptop
      %data0 = OpLoad %uint %buf Volatile|Aligned 4
      %data1 = OpLoad %uint %buf_1 Volatile|Aligned 4
        %sum = OpIAdd %uint %data0 %data1
               OpStore %buf %sum Volatile|Aligned 4
     %next_i = OpIAdd %uint %i %uint_1
      %data2 = OpLoad %uint %buf_2 Volatile|Aligned 4
   %looptest = OpULessThan %bool %next_i %data2
               OpBranchConditional %looptest %looptop %loopend
    %loopend = OpLabel
        %end = OpReadClockKHR %v2uint %scope_subgroup
   %start_hi = OpCompositeExtract %uint %start 1
   %start_lo = OpCompositeExtract %uint %start 0
     %end_hi = OpCompositeExtract %uint %end 1
     %end_lo = OpCompositeExtract %uint %end 0
                ; test_hi_lt = end_hi < start_hi
 %test_hi_lt = OpULessThan %bool %end_hi %start_hi
                ; test_hi_eq = end_hi == start_hi
 %test_hi_eq = OpIEqual %bool %end_hi %start_hi
                ; test_lo_lt = end_lo < start_lo
 %test_lo_lt = OpULessThan %bool %end_lo %start_lo
                ; test_lo = end_hi == start_hi && end_lo < start_lo
    %test_lo = OpLogicalAnd %bool %test_hi_eq %test_lo_lt
                ; test = end_hi < start_hi || (end_hi == start_hi && end_lo < start_lo)
       %test = OpLogicalOr %bool %test_hi_lt %test_lo
                ; pass if test is false
               OpBranchConditional %test %funcend %writepass
  %writepass = OpLabel
               OpStore %buf_3 %uint_1 Volatile|Aligned 4
               OpBranch %funcend
    %funcend = OpLabel
               OpReturn
               OpFunctionEnd
